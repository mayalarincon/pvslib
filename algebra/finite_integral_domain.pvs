%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brasil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brasil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brasil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brasil
%%----------------------------------------------------------------------------
%%
%%	Formalization of some results about finite integral domain
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

finite_integral_domain[T:Type+,+,*:[T,T->T],zero:T,one:T] : THEORY

  BEGIN

   ASSUMING IMPORTING  field_def[T,+,*,zero,one],
                       integral_domain_def[T,+,*,zero]

   	fullset_is_ring_with_one: ASSUMPTION ring_with_one?[T,+,*,zero,one](fullset[T])
	fullset_is_integral_domain: ASSUMPTION integral_domain?(fullset[T])
        
   ENDASSUMING

   IMPORTING integral_domain[T,+,*,zero],
             ring_nz_closed[T,+,*,zero],
             groupoid[T,+],
 	     groupoid[T,*],
             ring_with_one_basic_properties[T,+,*,zero,one],
	     zero_ring[T,+,*,zero]
	     

   n: VAR nat
   K : VAR set[T]
   nzx: VAR nz_T
   y, z: VAR T

%%----------------------------------------------------------------------------
%% Lemma about finite sets
%%
%% Generalization of the lemma "surj_equiv_inj" from the prelude file,
%% which holds for subsets of naturals
%%----------------------------------------------------------------------------

 surj_equiv_inj_fin_sets: LEMMA
   is_finite(K) IMPLIES
   FORALL (f: [(K) -> (K)]): injective?(f) IFF surjective?(f)

%%----------------------------------------------------------------------------
%% Definition of finite integral domain
%% and related results
%%----------------------------------------------------------------------------

  
  finite_integral_domain?(K) : bool =
      integral_domain?[T,+,*,zero](K) AND is_finite[T](K)

  S: VAR (finite_integral_domain?)

 %%----------------------------------------------------------------------------
 %% The cardinality of a ring must be greater than one in order 
 %% to justify that a finite integral domain is a field, since the 
 %% zero ring is the former but not the last.
 %%----------------------------------------------------------------------------

   zero_ring_is_fin_int_dom: LEMMA
      groupoid?[T,*](singleton(zero)) AND
      groupoid?[T,+](singleton(zero)) IFF
      finite_integral_domain?(singleton(zero))
 %%----------------------------------------------------------------------------

  nzx_member_S: LEMMA
      Card(S) > 1 IFF EXISTS nzx: member(nzx,S)

  auxiliar_map: LEMMA
      member(nzx,S) IMPLIES
      ( LET f:[(S) -> (S)] = LAMBDA (y:(S)): nzx * y IN
 	   bijective?(f))

  building_one: LEMMA
      Card(S) > 1 IMPLIES
      EXISTS (y:(S)): FORALL (z:(S)): z * y = z AND y * z = z

  one_is_member_S: LEMMA
      Card(S) > 1 IMPLIES member(one, S)

  fin_int_domain_is_ring_with_one: LEMMA
      FORALL (S: (finite_integral_domain?)):
        Card(S) > 1 IMPLIES ring_with_one?[T,+,*,zero,one](S)

  fin_int_domain_is_mult_group: LEMMA
     FORALL (S: (finite_integral_domain?)):
      Card(S) > 1 IMPLIES
      group?[T,*,one](remove(zero,S))

  fin_int_domain_is_field: LEMMA
      FORALL (S: (finite_integral_domain?)):
      Card(S) > 1 IMPLIES field?(S)


END finite_integral_domain
%%----------------------------------------------------------------------------
